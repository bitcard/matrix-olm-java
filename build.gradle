group 'msrd0.matrix'

buildscript {
	repositories {
		mavenLocal()
		jcenter()
		maven { url "https://msrd0.duckdns.org/artifactory/gradle" }
	}
	dependencies {
		// jni plugin
		classpath "msrd0.jni:jni-multilib-gradle:0.1-SNAPSHOT"
		
		// jfrog artifactory
		classpath "org.jfrog.buildinfo:build-info-extractor-gradle:+"
	}
}

apply plugin: 'java'
apply plugin: 'gradle-jni-multilib'

repositories {
	mavenLocal()
	jcenter()
	maven { url "https://msrd0.duckdns.org/artifactory/gradle" }
}

dependencies {
	// jni utils
	compile "msrd0.jni:jni-multilib:0.1-SNAPSHOT"
	
	// json lib
	compile "com.beust:klaxon:0.31-SNAPSHOT"
	
	// logging lib
	compile "org.slf4j:slf4j-api:1.7.+"
}

sourceSets {
	main {
		java.srcDirs = ['jni/java']
		resources.srcDirs = ['res/']
	}
}

jni {
	libraryName = "olm"
	
	sourceDirs = ["jni/native", "olm/src", "olm/lib"]
	includePath = ["olm/include"]
}

def olmLibVersion = "2.2.2"
version "$olmLibVersion-$matrix_olm_java_rel"


apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			pom.withXml {
				asNode()
					.appendNode('licenses')
						.appendNode('license')
							.appendNode('name', "Apache License, Version 2.0")
								.parent()
							.appendNode('url', "https://apache.org/licenses/LICENSE-2.0.txt")
								.parent()
							.appendNode('distribution', "repo")
								.parent()
							.appendNode('comments', "An open source license that alows proprietary use")
			}
		}
	}
}

artifactory {
	contextUrl = 'https://msrd0.duckdns.org/artifactory'
	resolve {
		repository {
			repoKey = 'gradle'
		}
	}
	publish {
		repository {
			repoKey = 'local'
			username = 'gitlab-ci'
			password = System.getenv("ARTIFACTORY_PASSWORD")
			maven = true
		}
		defaults {
			publications('mavenJava')
		}
	}
}

artifactoryPublish {
	dependsOn jar
}
