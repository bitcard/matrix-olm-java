import msrd0.jni.jni_multilib.*

buildscript {
	repositories {
		mavenLocal()
		jcenter()
		maven { url "https://maven.devpi.de/repository/default/" }
	}
	dependencies {
		// jni plugin
		classpath "msrd0.jni:jni-multilib-gradle:0.1-SNAPSHOT"
	}
}

apply plugin: 'java'
apply plugin: 'gradle-jni-multilib'

group 'msrd0.matrix'
version "$matrix_olm_major.$matrix_olm_minor.$matrix_olm_patch-$matrix_olm_java_rel"

repositories {
	mavenLocal()
	jcenter()
	maven { url "https://maven.devpi.de/repository/default/" }
}

dependencies {
	// jni utils
	compile "msrd0.jni:jni-multilib:0.1-SNAPSHOT"
	
	// json lib
	compile "com.beust:klaxon:0.31"
	
	// logging lib
	compile "org.slf4j:slf4j-api:1.7.+"
}

sourceSets {
	main {
		java.srcDirs = ['jni/java']
		resources.srcDirs = ['res/']
	}
}

jni {
	libraryName = "olm"
	
	sourceDirs = ["jni/native", "olm/src"]
	sourceFiles = ["olm/lib/crypto-algorithms/sha256.c", "olm/lib/crypto-algorithms/aes.c", "olm/lib/curve25519-donna/curve25519-donna.c"]
	includePath = ["olm/include", "olm/lib"]
	
	customFlags = [
			"-DOLMLIB_VERSION_MAJOR=$matrix_olm_major",
			"-DOLMLIB_VERSION_MINOR=$matrix_olm_minor",
			"-DOLMLIB_VERSION_PATCH=$matrix_olm_patch"
	]
	
	arch = [
			new Arch(OS.LINUX, Architecture.X86_64),
			new Arch(OS.LINUX, Architecture.X86),
			new Arch(OS.LINUX, Architecture.ARMv7),
			new Arch(OS.WINDOWS, Architecture.X86_64),
			new Arch(OS.WINDOWS, Architecture.X86)
	]
}

task sourcesJar(type : Jar) {
	from sourceSets.main.allSource
}


apply plugin: 'maven-publish'

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			
			artifact sourcesJar {
				classifier "sources"
			}
			
			pom.withXml {
				asNode()
					.appendNode('licenses')
						.appendNode('license')
							.appendNode('name', "Apache License, Version 2.0")
								.parent()
							.appendNode('url', "https://apache.org/licenses/LICENSE-2.0.txt")
								.parent()
							.appendNode('distribution', "repo")
								.parent()
							.appendNode('comments', "An open source license that alows proprietary use")
			}
		}
	}
	
	repositories {
		maven {
			url "https://maven.devpi.de/repository/default/"
			credentials(PasswordCredentials) {
				username "gitlab-ci"
				password System.getenv("ARTIFACTORY_PASSWORD")
			}
		}
	}
}
